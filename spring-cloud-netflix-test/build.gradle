
//应用于gradle 编译
buildscript {

    ext {
        set('springCloudVersion', "Hoxton.SR1")
        set('springBootVersion',"2.2.2.RELEASE")
    }

    repositories {
        mavenLocal()
        maven{ url 'https://maven.aliyun.com/repository/public/' }

        //maven 中央仓库
        mavenCentral()
    }

    dependencies {
        // 指定gradle spring boot plugin 版本 用于spring boot 版本依赖控制
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {

    group = 'top.quyq.netflix'
    version = '0.0.1-SNAPSHOT'

    description 'netflix projec'

    //指定中央仓库 项目使用
    repositories {
        mavenLocal()
        maven{ url 'https://maven.aliyun.com/repository/public/' }
        //maven 中央仓库
        mavenCentral()
    }
}


def holderProjects = Arrays.asList('service-discovery','consomer')

configure(subprojects) { subProject ->
    if( !holderProjects.contains(subProject.name)){
        apply plugin: 'java'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        sourceCompatibility = '1.8'

        dependencies {

            testImplementation('org.springframework.boot:spring-boot-starter-test') {
                exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
            }
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            }
        }

        test {
            useJUnitPlatform()
        }

    }
}
